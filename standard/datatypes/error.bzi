require "std:datatypes/unit"
require "std:datatypes/optional"
require "std:io"
require "std:math"

pub type Throwable<A> {
  Ok(x: A),
  Error(err: string)
}

pub let { Ok, Error } = Throwable

pub fn Throwable::map<A, B>(self, f: fn(A): B): Throwable<B> =>
  match self {
    case Ok(x) => Ok(f(x))
    case Error(err) => Error(err)
  }

pub fn Throwable::bind<A, B>(self, f: fn(A): Throwable<B>): Throwable<B> =>
  match self {
    case Ok(x) => f(x)
    case Error(err) => Error(err)
  }

pub fn Throwable::andThen<A, B>(self: Throwable<A>, other: Throwable<B>): Throwable<B> =>
  match self {
    case Ok(_) => other
    case Error(err) => Error(err)
  }

pub fn Throwable::pure<A>(x: A): Throwable<A> =>
  Ok(x)

pub fn Throwable::catch<A>(self: Throwable<A>, f: fn(string): Throwable<A>): Throwable<A> =>
  match self {
    case Ok(x) => Ok(x)
    case Error(err) => f(err)
  }

pub fn Throwable::catchError<A>(self: Throwable<A>, f: fn(string): A): A =>
  match self {
    case Ok(x) => x
    case Error(err) => f(err)
  }

pub fn isError<A>(self: Throwable<A>): bool =>
  match self {
    case Ok(_) => false
    case Error(_) => true
  }

pub fn isOk<A>(self: Throwable<A>): bool =>
  match self {
    case Ok(_) => true
    case Error(_) => false
  }

extern fn toString<A>(x: A): string

pub fn Throwable::unwrap<A>(self: Throwable<A>): A =>
  match self {
    case Ok(x) => x
    case Error(err) => panic("Error: $err")
  }